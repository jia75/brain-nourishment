[
    This program is a compiler that takes as input some stream of text that is
    valid brainf*ck and outputs c code that runs the content of the specified
    program.
]

First task: output the includes

>++++[<++++++++>-]<                     Save space character in cell 0

>+++++[>+++++++<-]>.                    Print # character
<+++++++[>++++++++++<-]>.               Print i character
+++++.-----------.+++++++++.+++++++++.  Print nclu
<++++++[>---<-]>+.+.<<.>>               Print "de "
<+++++[>--------<-]>-.                  Print ascii 60 (open angle bracket)

<+++++[>+++++++++++<-]>.+.              Print "st"
<++++[>----<-]>.+++++.++++++.           Print "dio"

>><+++++++++[>+++++<-]>+.[-]<<          Print a period without changing cell 3
-------.                                Print "h"
<+++++[>--------<-]>--.                 Print ascii 62 (closed angle bracket)

>++++++++++.<                           Print newline and store it in cell 3

[-]                                     Reset cell 2

<+++++[>+++++++<-]>.                    Print # character
<+++++++[>++++++++++<-]>.               Print i character
+++++.-----------.+++++++++.+++++++++.  Print nclu
<++++++[>---<-]>+.+.<<.>>               Print "de "
<+++++[>--------<-]>-.                  Print ascii 60 (open angle bracket)

<+++++[>+++++++++++<-]>.+.              Print "st"
<++++[>----<-]>.++++++++.---.-------.   Print "dlib"

>>><+++++++++[>+++++<-]>+.[-]<<<        Print a period without changing cell 3
++++++.                                 Print "h"
<+++++[>--------<-]>--.                 Print ascii 62 (closed angle bracket)

>.<                                     Print newline

<                                       Go to c1 which is empty
+++++++++++++++++++++++++++++++++++.++++Print "#include memory dot h"
++++++++++++++++++++++++++++++++++++++++
++++++++++++++++++++++++++.+++++.-------
----.+++++++++.+++++++++.---------------
--.+.-----------------------------------
----------------------------------.+++++
+++++++++++++++++++++++.++++++++++++++++
+++++++++++++++++++++++++++++++++.------
--.++++++++.++.+++.+++++++.-------------
----------------------------------------
----------------------.+++++++++++++++++
++++++++++++++++++++++++++++++++++++++++
+.--------------------------------------
----.-----------------------------------
-----------------.
[-]>                                    Zero c1 and go back to c2

Reference for the state of the cells:

0   1   2   3   4   5   6
32  0   62  10  0   0   0
        ptr

Second task: output "int main(){uint8_t*m=malloc("

<++++++[>+++++++<-]>+.+++++.++++++.     Print "int"
<<.>>                                   Print space
-------.------------.++++++++.+++++.    Print "main"
>>+++++[>++++++++<-]>.+.[-]<<<          Print "()"
+++++++++++++.------.------------.      Print "{ui"
+++++.++++++.<++++++[>----------<-]>.   Print "nt8"

<++++++++[>+++++<-]>-.<+++++[>++++<-]>+.Print "_t"

[-]                                     Reset c2

++++++++++++++++++++++++++++++++++++++++Print "*m=malloc(" using printer
++.+++++++++++++++++++++++++++++++++++++
++++++++++++++++++++++++++++++.---------
---------------------------------------.
++++++++++++++++++++++++++++++++++++++++
++++++++.------------.+++++++++++..+++.-
-----------.----------------------------
-------------------------------.

++++++++++.--.++++.++++.--------------.+Print "2048*sizeof(uint8_t));memset(mCO
++++++++++++++++++++++++++++++++++++++++    MMA0COMMAsizeof(uint8_t(*2048);int 
++++++++++++++++++++++++++++++++.-------    i=0;"
---.+++++++++++++++++.------------------
---.++++++++++.---------.---------------
----------------------------------------
-------.++++++++++++++++++++++++++++++++
++++++++++++++++++++++++++++++++++++++++
+++++.------------.+++++.++++++.--------
----------------------------------------
------------.+++++++++++++++++++++++++++
++++++++++++.+++++++++++++++++++++.-----
----------------------------------------
------------------------------..++++++++
++++++++++.+++++++++++++++++++++++++++++
+++++++++++++++++++++.--------.++++++++.
++++++.--------------.+++++++++++++++.--
----------------------------------------
----------------------------------.+++++
++++++++++++++++++++++++++++++++++++++++
++++++++++++++++++++++++.---------------
----------------------------------------
----------.++++.----.+++++++++++++++++++
++++++++++++++++++++++++++++++++++++++++
++++++++++++.----------.++++++++++++++++
+.---------------------.++++++++++.-----
----.-----------------------------------
---------------------------.++++++++++++
++++++++++++++++++++++++++++++++++++++++
+++++++++++++++++++++++++.------------.+
++++.++++++.----------------------------
--------------------------------.+++++++
++++++++++++++++++++++++++++++++.+++++++
++++++++++++++.-------------------------
----------------------------------------
----------.+.++++++++.--.++++.++++.---
------------.++++++++++++++++++.++++++++
++++++++++++++++++++++++++++++++++++++.+
++++.++++++.----------------------------
----------------------------------------
----------------.+++++++++++++++++++++++
++++++++++++++++++++++++++++++++++++++++
++++++++++.-----------------------------
---------------.-------------.++++++++++
+.

>[-]<                                   Set c3 to 0

[-]                                     Set c2 to 0

+[                                      Start reading loop off c2
    >,                                  Read into c3
    >++                                 Set c4 to 2 signifying that no end
                                            characters have been found
    >+                                  Set c5 to 1 signifying that no 
                                            continuing characters have been
                                            found
    >>>+                                Set c8 to 1 signifying that no plus has
                                            been found
    >+                                  Set c9 to 1 signifying that no comma 
                                            has been found
    >+                                  Set c10 to 1 signifying that no dash
                                            has been found
    >+                                  Set c11 to 1 signifying that no period
                                            has been found
    >+                                  Set c12 to 1 signifying that no open
                                            angle bracket has been found
    >+                                  Set c13 to 1 signifying that no closed
                                            angle bracket has been found
    >+                                  Set c14 to 1 signifying that no open
                                            square bracket has been found
    >+                                  Set c15 to 1 signifying that no closed
                                            square bracket has been found

    Helpful table for every cell and what values they hold:

    | Cell | Character     | Found | Not Found |
    | ==== | ============= | ===== | ========= |
    | c8   | plus          | 1     | 0         |
    | c9   | comma         | 1     | 0         |
    | c10  | minus         | 1     | 0         |
    | c11  | period        | 1     | 0         |
    | c12  | open angle    | 1     | 0         |
    | c13  | closed angle  | 1     | 0         |
    | c14  | open square   | 1     | 0         |
    | c15  | closed square | 1     | 0         |

    <<<<<<<<<<<<                        Go to c3

    
    [->>>+>+<<<<]>>>>[-<<<<+>>>>]<      Copy c3 to c6 via c7

    For reference: ptr is at c6

    +[                                  If c6 is not 255 (aka EOF in some unix)
        <<->>                           Decrement c4
        [-]                             Reset c6
    ]

    <<<[->>>+>+<<<<]>>>>[-<<<<+>>>>]<   Copy c3 to c6 via c7
    ----[                               If c6 is not 4 (aka EOT in ascii)
        <<->>                           Decrement c4 (if neither char has been
                                            found c4 will be 0; if either has
                                            been found it will be 1)
        [-]                             Reset c6
    ]

    <<<[->>>+>+<<<<]>>>>[-<<<<+>>>>]<   Copy c3 to c6 via c7
    ------------------------------------If c6 is not 43 (aka plus in ascii)
    -------[                            
        <[-]                            Set c5 to 0
        >>>[-]<<                        Set c8 to 0
        [-]                             Reset c6
    ]

    <<<[->>>+>+<<<<]>>>>[-<<<<+>>>>]<   Copy c3 to c6 via c7
    ------------------------------------If c6 is not 44 (aka comma in ascii)
    --------[                            
        <[-]                            Set c5 to 0
        >>>>[-]<<<                      Set c9 to 0
        [-]                             Reset c6
    ]

    <<<[->>>+>+<<<<]>>>>[-<<<<+>>>>]<   Copy c3 to c6 via c7
    ------------------------------------If c6 is not 45 (aka dash in ascii)
    ---------[                            
        <[-]                            Set c5 to 0
        >>>>>[-]<<<<                    Set c10 to 0
        [-]                             Reset c6
    ]

    <<<[->>>+>+<<<<]>>>>[-<<<<+>>>>]<   Copy c3 to c6 via c7
    ------------------------------------If c6 is not 46 (aka period in ascii)
    ----------[                            
        <[-]                            Set c5 to 0
        >>>>>>[-]<<<<<                  Set c11 to 0
        [-]                             Reset c6
    ]

    <<<[->>>+>+<<<<]>>>>[-<<<<+>>>>]<   Copy c3 to c6 via c7
    ------------------------------------If c6 is not 60 (aka open angle bracket
    ------------------------[                in ascii)
        <[-]                            Set c5 to 0
        >>>>>>>[-]<<<<<<                Set c12 to 0
        [-]                             Reset c6
    ]

    <<<[->>>+>+<<<<]>>>>[-<<<<+>>>>]<   Copy c3 to c6 via c7
    ------------------------------------If c6 is not 62 (aka closed angle
    --------------------------[              bracket in ascii)
        <[-]                            Set c5 to 0
        >>>>>>>>[-]<<<<<<<              Set c13 to 0
        [-]                             Reset c6
    ]

    <<<[->>>+>+<<<<]>>>>[-<<<<+>>>>]<   Copy c3 to c6 via c7
    ------------------------------------If c6 is not 91 (aka open square
    ------------------------------------    bracket in ascii)
    -------------------[             
        <[-]                            Set c5 to 0
        >>>>>>>>>[-]<<<<<<<<            Set c14 to 0
        [-]                             Reset c6
    ]

    <<<[->>>+>+<<<<]>>>>[-<<<<+>>>>]<   Copy c3 to c6 via c7
    ------------------------------------If c6 is not 93 (aka closed square
    ------------------------------------    bracket in ascii)
    ---------------------[             
        <[-]>                           Set c5 to 0
        >>>>>>>>>[-]<<<<<<<<<           Set c15 to 0
        [-]                             Reset c6
    ]

    For reference: ptr is at c6

    <<[                                 Go to c4 to check if any end chars
        <<[-]>>                         Zero c2 to end the outer loop
        [-]                             Zero c4 to exit this loop
    ]

    >>>>[                               Check c8 for plus character
        [-]                             Zero c8 to have space to print
        ++++++++++++++++++++++++++++++++
        +++++++++++..+++++++++++++++++++
        ++++++++++++++++++++++++++++++++
        +++++++++++++++.----------------
        --.++++++++++++++.------------.-
        --------------------------------
        -.
        [-]                             Zero c8 again to exit the loop
    ]

    >[                                  Check c9 for comma character
        [-]                             Zero c9 to have space to print
        ++++++++++++++++++++++++++++++++
        ++++++++++++++++++++++++++++++++
        ++++++++++++++++++++++++++++++++
        +++++++++++++.------------------
        .++++++++++++++.------------.---
        -----------------------------.--
        -------------------.++++++++++++
        ++++++++++++++++++++++++++++++++
        ++++++++++++++++++++++++++++++++
        +.------------.+++++.++++++.----
        --------------------------------
        ------------------------.+++++++
        ++++++++++++++++++++++++++++++++
        .+++++++++++++++++++++.---------
        --------------------------------
        --------------------------------
        --.+++++++++++++++++++++++++++++
        ++++++++++++++++++++++++++++++++
        +.--.+++++++++++++++.-----------
        ------.-------------------------
        --------------------------------
        --.+++++++++++++++++++++++++++++
        ++++++++++++++++++++++++++++++++
        ++++++++++++++.+.---------------
        -.+++++.+++++.------------------
        --------------------------------
        -------------------.++++++++++++
        ++++++.
        [-]                             Zero c9 again to exit the loop
    ]

    >[                                  Check c10 for minus character
        [-]                             Zero c10 to have space to print
        ++++++++++++++++++++++++++++++++
        +++++++++++++..+++++++++++++++++
        ++++++++++++++++++++++++++++++++
        +++++++++++++++.----------------
        --.++++++++++++++.------------.-
        --------------------------------
        -.
        [-]                             Zero c10 again to exit the loop
    ]

    >[                                  Check c11 for period character
        [-]                             Zero c11 to have space to print
        ++++++++++++++++++++++++++++++++
        ++++++++++++++++++++++++++++++++
        ++++++++++++++++++++++++++++++++
        ++++++.++++++++++.+++++.-.------
        -----------.--------------------
        --------------------------------
        -------..+++++++++++++++++++++++
        ++++++++++++++++++++++++++++++++
        ++++.+++++.-------.+++++++++++++
        ++++.---------------------------
        --------------------------------
        --------------.+++++++++++++++++
        ++++++++++++++++++++++++++++++++
        +++++++++++++++++++.------------
        ------.++++++++++++++.----------
        --.-----------------------------
        --------------------.+++++++++++
        ++++++++++++++++++++++++++++++++
        ++++++++++++++++++++++++++++.+.-
        ---------------.+++++++++++.++++
        ++.-.---------------------------
        --------------------------------
        ----------------.+++++++++++++++
        +++.
        [-]                             Zero c11 again to exit the loop
    ]

    >[                                  Check c12 for open angle character
        [-]                             Zero c12 to have space to print
        ++++++++++++++++++++++++++++++++
        +++++++++++++..+++++++++++++++++
        ++++++++++++++++++++++++++++++++
        +++++++++++.--------------------
        --------------------------.
        [-]                             Zero c12 again to exit the loop
    ]

    >[                                  Check c13 for closed angle character
        [-]                             Zero c13 to have space to print
        ++++++++++++++++++++++++++++++++
        +++++++++++..+++++++++++++++++++
        ++++++++++++++++++++++++++++++++
        +++++++++++.--------------------
        --------------------------.
        [-]                             Zero c13 again to exit the loop
    ]

    >[                                  Check c14 for open square character
        [-]                             Zero c14 to have space to print
        ++++++++++++++++++++++++++++++++
        ++++++++++++++++++++++++++++++++
        ++++++++++++++++++++++++++++++++
        +++++++++++++++++++++++.--------
        -------.+.+++.-------.----------
        --------------------------------
        -------------------.++++++++++++
        ++++++++++++++++++++++++++++++++
        +++++++++++++++++++++++++.------
        ------------.++++++++++++++.----
        --------.-----------------------
        -----------------------------.++
        ++++++++++++++++++++++++++++++++
        ++++++++++++++++++++++++++++++++
        ++++++++++++++++.
        [-]                             Zero c14 again to exit the loop
    ]

    >[                                  Check c15 for closed square character
        [-]                             Zero c15 to have space to print
        ++++++++++++++++++++++++++++++++
        ++++++++++++++++++++++++++++++++
        ++++++++++++++++++++++++++++++++
        +++++++++++++++++++++++++++++.
        [-]                             Zero c15 again to exit the loop
    ]

    <<<<<<<<<<<<<                       Go back to c2
]

[-]
++++++++++++++++++++++++++++++++++++++++Print "free(m);return 0;}" and a
++++++++++++++++++++++++++++++++++++++++    newline
++++++++++++++++++++++.++++++++++++.----
---------..-----------------------------
--------------------------------.+++++++
++++++++++++++++++++++++++++++++++++++++
++++++++++++++++++++++.-----------------
----------------------------------------
-----------.++++++++++++++++++.+++++++++
++++++++++++++++++++++++++++++++++++++++
++++++.-------------.+++++++++++++++.+.-
--.----.--------------------------------
----------------------------------------
------.++++++++++++++++.+++++++++++.++++
++++++++++++++++++++++++++++++++++++++++
++++++++++++++++++++++.-----------------
----------------------------------------
----------------------------------------
------------------.
